旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴커
;VGA_16 ..................... LIBRARY ROUTINE rev 001, by Jens-Christian Huus 
쳐컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴캑
;General video routines used globally like SETCOLOR, RASTER, VERTICAL, BLANK, 
;PALETTE, FADEOUT and HORIZONTAL. Use in 16 colors mode only.                 
읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸




컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
SEGMENT         Data    PUBLIC 'Data'

PalCount        db      0,1,2,3,4,5,20,7,56,57,58,59,60,61,62,63

PalBuffer       dw      48 dup (?)

ENDS
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
SEGMENT         Code    PUBLIC 'Code'

Blank:                                          ;Toggle the display ON or OFF.
                push    ax dx

                mov     dx,3c4h
                mov     al,1
                out     dx,al
                inc     dx
                in      al,dx
                xor     al,00100000b
                out     dx,al

                pop     dx ax
                ret
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
Raster:                                         ;Set new OVERSCAN color.
                push    ax dx

                mov     dx,3dah
                in      al,dx
                nop
                mov     dx,3c0h
                in      al,dx
                mov     al,31h
                nop
                out     dx,al
                mov     al,bl
                nop
                out     dx,al

                pop     dx ax
                ret
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
SetColor:                                       ;Set color register's new RGB.
                push    ax dx

                mov     dx,3c8h                 ;AL = Color register to set.
                out     dx,al                   ;AH = Red.
                inc     dx                      ;BL = Green.
                nop                             ;BH = Blue.
                mov     al,ah
                out     dx,al
                nop
                nop
                mov     al,bl
                out     dx,al
                nop
                nop
                mov     al,bh
                out     dx,al

                pop     dx ax
                ret
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
Palette:                                        ;Set all 16 colors in a palette.
                push    ax bx cx si di

                xor     si,si
                mov     cx,16                   ;DI = Offset to palette table.
@@p10:          mov     ah,[di]
                mov     bx,[di+1]
                mov     al,[PalCount+si]
                call    SetColor
                inc     di
                inc     di
                inc     di
                inc     si
                loop    @@p10

                pop     di si cx bx ax
                ret
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
FadeOut:                                        ;Fade all 16 colors to zero.
                pushf
                push    ax bx cx dx di bp ds es

                cld                             ;BX = Fade speed...
                mov     ax,ds                   ;DI = Offset to palette table.
                mov     es,ax

                mov     bp,di

                mov     si,di
                lea     di,[PalBuffer]
                mov     cx,48
@@f10:          lodsb
                mov     ah,al
                xor     al,al
                shl     ah,1
                shl     ah,1
                stosw
                loop    @@f10

            mov     bx,30
@@f12:
                lea     di,[PalBuffer]
                mov     cx,49
@@f20:          rep     scasw
                jcxz    @@f30
                mov     ax,[di-2]               ;Use dividing by frame method.
                push    ax
                xor     dx,dx
                div     bx
                pop     dx
                sub     dx,ax
                mov     [di-2],dx
                shr     dh,1                    ;Fint Jens, men der skal IKKE
                shr     dh,1                    ;divideres hver gang!
                mov     si,di
                sub     si,offset PalBuffer
                shr     si,1
                dec     si
                mov     [ds:bp+si],dh
                jmp     short @@f20
@@f30:
                jz      @@f40
                call    Vertical
                mov     di,bp
                call    Palette
                jmp     short @@f12
@@f40:
                pop     es ds bp di dx cx bx ax
                popf
                ret
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
Vertical:                                       ;Wait for vertical retrace.
                push    ax dx

                mov     dx,3dah
@@v10:          in      al,dx
                test    al,8
                jz      @@v10
                cli
@@v20:          in      al,dx
                test    al,8
                jnz     @@v20
                sti

                pop     dx ax
                ret
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
Horizontal:                                     ;Wait for horizontal retrace.
                cli
                mov     dx,3dah
@@h10:          in      al,dx
                test    al,1                    ;PUSH/POP not used, for speed.
                jz      @@h10
@@h20:          in      al,dx
                test    al,1
                jnz     @@h20
                sti
                ret
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
ENDS
