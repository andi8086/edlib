旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴커
;VIDEO016 ................... LIBRARY ROUTINE rev 001, by Jens-Christian Huus 
쳐컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴캑
;General video routines used globally like SETCOLOR, RASTER, VERTICAL, BLANK, 
;PALETTE, FADEOUT and HORIZONTAL. Use in 16 colors modes only.                
읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸




컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
SEGMENT         Data    PUBLIC 'Data'

PalCount        db      0,1,2,3,4,5,20,7,56,57,58,59,60,61,62,63

ENDS
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
SEGMENT         Code    PUBLIC 'Code'


Blank:
旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴커
;Toggles the display screen ON or OFF.                                        
;                                                                             
읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸
                push    ax dx

                mov     dx,3c4h
                mov     al,1
                out     dx,al
                inc     dx
                in      al,dx
                xor     al,00100000b
                out     dx,al

                pop     dx ax
                ret
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴

Raster:
旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴커
;Sets the OVERSCAN color, useful when determining CPU usage.                  
;                                                                             
쳐컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴캑
;PARAMETERS:   BL = Overscan color.                                           
읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸
                push    ax dx

                mov     dx,3dah
                in      al,dx
                nop
                mov     dx,3c0h
                in      al,dx
                mov     al,31h
                nop
                out     dx,al
                mov     al,bl
                nop
                out     dx,al

                pop     dx ax
                ret
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴

SetColor:
旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴커
;Set a new RGB value for a specific color register. Don't remove the NOP's as 
;they make sure the timing doesn't get too fast - Some PC's needs that delay! 
쳐컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴캑
;PARAMETERS:   AL = Color register to set.                                    
;              AH = Red.                                                      
;              BL = Green.                                                    
;              BH = Blue.                                                     
읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸
                push    ax dx

                mov     dx,3c8h
                out     dx,al
                inc     dx
                nop
                mov     al,ah
                out     dx,al
                nop
                nop
                mov     al,bl
                out     dx,al
                nop
                nop
                mov     al,bh
                out     dx,al

                pop     dx ax
                ret
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴

Palette:
旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴커
;Sets all 16 color registers in the palette (768 bytes) - needless to say, it 
;only works in 16 colors modes. The routine works on older PC's with problems 
;concerning the speed making it more reliable than function 1012h in INT 10h. 
쳐컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴캑
;PARAMETERS:   DI = Offset to palette table (768 bytes) in R,G,B, R,G,B ...   
읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸
                push    ax bx cx si

                xor     si,si
                mov     cx,16
@@p10:          mov     ah,[di]
                mov     bx,[di+1]
                mov     al,[PalCount+si]
                call    SetColor
                inc     di
                inc     di
                inc     di
                inc     si
                loop    @@p10

                pop     si cx bx ax
                ret
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴

FadeOut:
旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴커
;Fades all 256 colors to black using the standard DECREASE method. Fading the 
;colors with VERTICAL makes it fade fast enough to look good in spite of this 
;simple method. Only works in 256 color modes.                                
쳐컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴캑
;PARAMETERS:   DI = Offset to palette table (768 bytes) in R,G,B, R,G,B ...   
읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸
                pushf
                push    ax cx si di ds es

                cld
                mov     ax,dx
                mov     es,ax
                mov     si,40h
@@f10:
                xor     al,al
                mov     cx,768
@@f20:          rep     scasb
                jcxz    @@f30
                dec     [byte di-1]
                jmp     short @@f20
@@f30:
                call    Vertical
                sub     di,768
                call    Palette
                sub     di,768
                dec     si
                jne     @@f10

                pop     es ds di si cx ax
                popf
                ret
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴

Vertical:
旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴커
;Waits for the vertical retrace to happen.                                    
;                                                                             
읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸
                push    ax dx

                mov     dx,3dah
@@v10:          in      al,dx
                test    al,8
                jz      @@v10
                cli
@@v20:          in      al,dx
                test    al,8
                jnz     @@v20
                sti

                pop     dx ax
                ret
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴

Horizontal:
旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴커
;Waits for the horizontal retrace to happen. Note - this routine doesn't PUSH 
;and POP any registers.                                                       
읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸
                cli
                mov     dx,3dah
@@h10:          in      al,dx
                test    al,1
                jz      @@h10
@@h20:          in      al,dx
                test    al,1
                jnz     @@h20
                sti
                ret

컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
ENDS
